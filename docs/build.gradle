plugins {
  id 'io.miret.etienne.sass'
  id 'java-library'
  id 'scala'
}

compileSass {
  group = 'publishing'

  version = '1.34.0'

  sourceDir = project.file("$projectDir/sass")
  outputDir = project.file("$projectDir/css")
}
site.dependsOn(compileSass)

dependencies {
  implementation project(':opentorah-site')
  implementation project(':opentorah-base')

  implementation project(':opentorah-calendar')

  implementation libs.logback
  implementation libs.logstash

  implementation libs.xmlresolver, libs.jing
  implementation libs.saxon10

  implementation libs.zhttp
}

task buildAndPrettyPrintSite(type: JavaExec) {
  group = 'publishing'
  dependsOn(compileSass)

  mainClass = 'org.opentorah.site.SiteServiceCommon'
  args = ['buildAndPrettyPrintSite', projectDir]
  classpath = sourceSets.main.runtimeClasspath
}
site.dependsOn(buildAndPrettyPrintSite)

task serveSite(type: JavaExec) {
  group = 'publishing'

  mainClass = 'org.opentorah.site.SiteServiceCommon'
  args = ['serveSite', "file://$projectDir/"]
  classpath = sourceSets.main.runtimeClasspath
}

pluginManagement {
  repositories {
    mavenCentral()
    gradlePluginPortal()
  }

  plugins {
    id 'com.github.ben-manes.versions' version '0.44.0'
    id 'org.podval.tools.scalajs'      version '0.4.13'
    id 'com.google.cloud.tools.jib'    version '3.4.4'
    id 'org.podval.tools.cloudrun'     version '0.4.0'
    id 'com.gradle.plugin-publish'     version '1.2.1'
  }
}

final String scalaVersion      = '3.6.2'
final String scalaVersionMajor = '3'

final String zioVersion        = '2.1.14'
final String flexmarkVersion   = '0.64.8'
final String catsVersion       = '2.12.0'

dependencyResolutionManagement {
  repositories {
    mavenCentral()
//    maven {
//      url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
//    }
  }

  // Yes, I know about 'libs.versions.toml', but:
  // - I do not want to use yet another configuration language;
  // - it doesn't allow me to interpolate 'scalaVersionMajor';
  // - this way both plugin and dependency versions are in the same file.
  versionCatalogs {libs {
    // Scala
    library('scala-library', "org.scala-lang:scala3-library_3:$scalaVersion")
    library('scala-xml'    , "org.scala-lang.modules:scala-xml_$scalaVersionMajor:2.3.0")
    library('zinc'         , 'org.scala-sbt:zinc_2.13:1.10.5')

    // XML
    library('xerces'       , 'xerces:xercesImpl:2.12.2')
    // tagsoup     = "1.2.1" TODO use org.jsoup?

    // Google Cloud
    library('google.cloud.storage', 'com.google.cloud:google-cloud-storage:2.46.0')

    // ZIO
    library('zio-core'     , "dev.zio:zio_$scalaVersionMajor:$zioVersion")
    library('zio-streams'  , "dev.zio:zio-streams_$scalaVersionMajor:$zioVersion")
    library('zio-test'     , "dev.zio:zio-test_$scalaVersionMajor:$zioVersion")
    library('zio-http'     , "dev.zio:zio-http_$scalaVersionMajor:3.0.1")

    // Algebra
    library('cats-core'    , "org.typelevel:cats-core_$scalaVersionMajor:$catsVersion")
    library('cats-laws'    , "org.typelevel:cats-laws_$scalaVersionMajor:$catsVersion")
    library('algebraLaws'  , "org.typelevel:algebra-laws_$scalaVersionMajor:$catsVersion")
    library('discipline-scalatest', "org.typelevel:discipline-scalatest_$scalaVersionMajor:2.3.0")

    // Paiges
    library('paiges'       , "org.typelevel:paiges-core_$scalaVersionMajor:0.4.4")

    // Caching
    // I didn't find [ScalaCache](https://cb372.github.io/scalacache/) documentation,
    // and since I am not at the point of using anything other than Caffeine,
    // I'll use it directly - albeit not Scala-idiomatically ;)
    library('caffeine'     , 'com.github.ben-manes.caffeine:caffeine:3.1.8')

    // Logging
    library('slf4j'   , 'org.slf4j:slf4j-api:2.0.16')
    library('logback' , 'ch.qos.logback:logback-classic:1.5.15')
    library('logstash', 'net.logstash.logback:logstash-logback-encoder:8.0')

    // Flexmark
    library('flexmark-core'           , "com.vladsch.flexmark:flexmark:$flexmarkVersion")
//    library('flexmark-profile-pegdown', "com.vladsch.flexmark:flexmark-profile-pegdown:$flexmarkVersion")
    library('flexmark-ext-toc'        , "com.vladsch.flexmark:flexmark-ext-toc:$flexmarkVersion")
    bundle('markdown', ['flexmark-core', 'flexmark-ext-toc'])

    // Zmanim
    library('zmanim', 'com.kosherjava:zmanim:2.5.0')

    // Testing
    library('scalatest' , "org.scalatest:scalatest_$scalaVersionMajor:3.2.19")
    library('scalacheck', "org.scalacheck:scalacheck_$scalaVersionMajor:1.18.1")
  }}
}

rootProject.name = 'opentorah'

include ':util'
project(':util'     ).name = 'opentorah-util'

include ':core'
project(':core'     ).name = 'opentorah-core'

include ':texts'
project(':texts'    ).name = 'opentorah-texts'

include ':collector'
project(':collector').name = 'opentorah-collector'

include ':docs'

plugins {
  id 'java-library'
  id 'scala'
  id 'com.github.maiflai.scalatest'
  id 'maven-publish'
  id 'signing'
  id 'com.google.cloud.tools.jib'
  id 'org.podval.tools.cloudrun'
}

description = 'Archive Collector'

apply from: '../configure.gradle'

dependencies {
  implementation project(':opentorah-base')
  implementation project(':opentorah-site')
  implementation project(':opentorah-tei')

  // ancient flexmark that scalatest requires uses ancient log4j that has a vulnerability that Maven Central blocks...
  // so I can't use the bundle:
  // compileOnly libs.bundles.markdown
  implementation (libs.flexmark.core) {
    exclude group: 'log4j', module: 'log4j'
  }
  implementation (libs.flexmark.ext.toc) {
    exclude group: 'com.vladsch.flexmark', module: 'flexmark'
  }

  implementation libs.zhttp
  implementation libs.logback
  implementation libs.logstash

  testImplementation libs.podvalRun
}

jib.from.image = 'adoptopenjdk:11'
jib.container.mainClass = 'org.opentorah.collector.Service'

cloudRun.region = 'us-east4'
deploy.dependsOn(cloudRunDeploy)

task cloudRunLocalFromLocalStore(type: org.podval.tools.cloudrun.CloudRunPlugin.RunLocalTask)
cloudRunLocalFromLocalStore.additionalOptions = [
  // --volume?
  '--mount', 'type=bind,source=/home/dub/OpenTorah/alter-rebbe.org/,target=/mnt/store/',
  '--env'  , 'STORE=file:///mnt/store/'
]
